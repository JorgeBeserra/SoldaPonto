/*********************************************
  Autor: Marlon Nardi Walendorff
  Projeto: Solda ponto para baterias de l√≠tio com Arduino
  Detalhes do projeto:https://marlonnardi.com/2023/12/03/como-fazer-solda-ponto-profissional-para-baterias-18650-e-mais-construa-sua-propria-bicicleta-eletrica-1/
**********************************************/

/*
  Atualiza√ßao: Jorge Souza
*/

//==================== Inclus√£o de Bibliotecas =================//
#include <Arduino.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1331.h>
#include <SPI.h>
#include <RotaryEncoder.h>
//--- WiFi e atualiza√ß√£o OTA ---//
#include <ESP8266WiFi.h>
#include <ESP8266WebServer.h>
#include <ESP8266HTTPUpdateServer.h>
#include <EEPROM.h>
#include <ESP8266httpUpdate.h>

#define FIRMWARE_VERSION "1.0.1"

//==================== Mapeamento de Hardware ==================//

#define pin_Trigger 12 // D6
#define pin_Triac 3 // RX

#define pin_Encoder_CLK     0  // D3
#define pin_Encoder_DT      2  // TX
#define pin_Encoder_SW     16  // D0

const uint8_t   OLED_pin_scl_sck        = 13; // D7
const uint8_t   OLED_pin_sda_mosi       = 14; // D5
const uint8_t   OLED_pin_cs_ss          = 15; // D8
const uint8_t   OLED_pin_res_rst        = 5;  // D1
const uint8_t   OLED_pin_dc_rs          = 4;  // D2

#define min_Time_ms 3
#define max_Time_ms 120

#define min_Cycle_ms 3
#define max_Cycle_ms 120

#define SCREEN_WIDTH 96 // OLED display width, in pixels
#define SCREEN_HEIGHT 64 // OLED display height, in pixels

#define EEPROM_WIFI_CONFIG_START 0
#define EEPROM_CYCLE_MS_ADDR 100
#define EEPROM_TIME_MS_ADDR 102
#define EEPROM_COUNTER_ADDR 104

volatile bool encoderFlag = false;

// SSD1331 color definitions
const uint16_t  OLED_Color_Black        = 0x0000;
const uint16_t  OLED_Color_Blue         = 0x001F;
const uint16_t  OLED_Color_Red          = 0xF800;
const uint16_t  OLED_Color_Green        = 0x07E0;
const uint16_t  OLED_Color_Cyan         = 0x07FF;
const uint16_t  OLED_Color_Magenta      = 0xF81F;
const uint16_t  OLED_Color_Yellow       = 0xFFE0;
const uint16_t  OLED_Color_White        = 0xFFFF;

// The colors we actually want to use
uint16_t        OLED_Text_Color         = OLED_Color_Black;
uint16_t        OLED_Background_Color    = OLED_Color_Blue;

enum MenuState { NORMAL, EDITANDO_CICLO, EDITANDO_TEMPO };
MenuState menuState = NORMAL;

unsigned long lastBlink = 0;
bool showValue = true;

bool lastButtonState = HIGH;
unsigned long lastButtonChange = 0;
const unsigned long debounceDelay = 150;


const char* githubAPI = "https://api.github.com/repos/JorgeBeserra/SoldaPonto/releases/latest";
const char* firmwareBinURL = "https://github.com/JorgeBeserra/SoldaPonto/releases/latest/download/SoldaPonto.bin";


//==================== Inst√¢nciando Objetos ====================//
Adafruit_SSD1331 display =  Adafruit_SSD1331(
        OLED_pin_cs_ss,
        OLED_pin_dc_rs,
        OLED_pin_sda_mosi,
        OLED_pin_scl_sck,
        OLED_pin_res_rst
     );

RotaryEncoder EncoderOne(pin_Encoder_CLK, pin_Encoder_DT);

RotaryEncoder *encoder = nullptr;

//==================== Vari√°veis Globais ==================//
byte aux2 = 0;

int16_t valorEncoder = 0;
uint16_t time_ms = 0;
uint16_t last_time_ms = 0;
uint16_t cycle_ms = 0;
uint16_t last_cycle_ms = 0;
uint32_t soldaCount = 0;
uint32_t last_soldaCount = 0;
static String last_wifi_status = "";

//------- Configura√ß√£o WiFi -------//
struct WifiConfig {
  char ssid[32];
  char pass[64];
};

WifiConfig wifiConfig;
bool configMode = false;

ESP8266WebServer server(80);
ESP8266HTTPUpdateServer httpUpdater;

void checkForUpdate() {
  if (WiFi.status() != WL_CONNECTED) {
    Serial.println("‚ùå Sem conex√£o Wi-Fi. Cancelando verifica√ß√£o.");
    return;
  }

  // Testa resolu√ß√£o DNS
  Serial.print("üîç Testando DNS para api.github.com...");
  IPAddress githubIP;
  if (!WiFi.hostByName("api.github.com", githubIP)) {
    Serial.println("‚ùå Falha na resolu√ß√£o DNS.");
    return;
  }
  Serial.print("‚úîÔ∏è Resolvido IP: ");
  Serial.println(githubIP);

  // Inicia conex√£o HTTPS
  WiFiClientSecure client;
  client.setInsecure();  // ‚ö†Ô∏è Ignora SSL (porque o ESP8266 n√£o tem espa√ßo para certificados)

  HTTPClient https;
  const char* url = "https://api.github.com/repos/JorgeBeserra/SoldaPonto/releases/latest";

  Serial.println("üîó Acessando GitHub API...");
  https.begin(client, url);
  https.addHeader("User-Agent", "ESP8266");  // ‚ö†Ô∏è GitHub exige User-Agent

  int httpCode = https.GET();

  if (httpCode > 0) {
    Serial.printf("üì° HTTP Code: %d\n", httpCode);
    if (httpCode == HTTP_CODE_OK) {
      String payload = https.getString();
      int index = payload.indexOf("\"tag_name\":\"v");
      if (index > 0) {
        int start = index + 13;
        int end = payload.indexOf("\"", start);
        String latestVersion = payload.substring(start, end);

        Serial.printf("‚úÖ √öltima vers√£o no GitHub: %s\n", latestVersion.c_str());
        Serial.printf("üîß Minha vers√£o atual: %s\n", FIRMWARE_VERSION);

        if (latestVersion != FIRMWARE_VERSION) {
          Serial.println("üöÄ Nova vers√£o encontrada! Iniciando atualiza√ß√£o OTA...");

          t_httpUpdate_return result = ESPhttpUpdate.update(
            client,
            "https://github.com/JorgeBeserra/SoldaPonto/releases/latest/download/SoldaPonto.bin"
          );

          switch (result) {
            case HTTP_UPDATE_FAILED:
              Serial.printf("‚ùå OTA falhou. Erro (%d): %s\n",
                            ESPhttpUpdate.getLastError(),
                            ESPhttpUpdate.getLastErrorString().c_str());
              break;

            case HTTP_UPDATE_NO_UPDATES:
              Serial.println("‚ö†Ô∏è Nenhuma atualiza√ß√£o dispon√≠vel.");
              break;

            case HTTP_UPDATE_OK:
              Serial.println("‚úÖ Atualiza√ß√£o conclu√≠da. Reiniciando...");
              break;
          }
        } else {
          Serial.println("üëç Firmware j√° est√° na √∫ltima vers√£o.");
        }
      } else {
        Serial.println("‚ùå N√£o foi poss√≠vel encontrar a tag da vers√£o na resposta.");
      }
    } else {
      Serial.printf("‚ö†Ô∏è GitHub respondeu HTTP %d\n", httpCode);
    }
  } else {
    Serial.printf("‚ùå Falha na conex√£o. Erro HTTP: %d\n", httpCode);
  }

  https.end();
}



void loadWifiConfig() {
  EEPROM.begin(512);
  EEPROM.get(EEPROM_WIFI_CONFIG_START, wifiConfig);
  if (wifiConfig.ssid[0] == 0xFF || wifiConfig.ssid[0] == '\0') {
    memset(&wifiConfig, 0, sizeof(WifiConfig));
  }
}

void saveWifiConfig() {
  EEPROM.begin(512);
  EEPROM.put(EEPROM_WIFI_CONFIG_START, wifiConfig);
  EEPROM.commit();
  Serial.println("üíæ Configura√ß√µes Wi-Fi salvas na EEPROM:");
  Serial.print("üî∏ SSID: ");
  Serial.println(wifiConfig.ssid);
  Serial.print("üî∏ Senha: ");
  Serial.println(wifiConfig.pass);
}

void saveSettings() {
  EEPROM.begin(512);
  EEPROM.put(EEPROM_CYCLE_MS_ADDR, cycle_ms);
  EEPROM.put(EEPROM_TIME_MS_ADDR, time_ms);
  EEPROM.commit();
  EEPROM.end();
}

void loadSettings() {
  EEPROM.begin(512);
  EEPROM.get(EEPROM_CYCLE_MS_ADDR, cycle_ms);
  EEPROM.get(EEPROM_TIME_MS_ADDR, time_ms);
  EEPROM.end();

  // Se os valores forem inv√°lidos, aplica padr√£o seguro
  if (cycle_ms < min_Cycle_ms || cycle_ms > max_Cycle_ms) cycle_ms = 30;
  if (time_ms < min_Time_ms || time_ms > max_Time_ms) time_ms = 30;
}

void saveCounter() {
  EEPROM.begin(512);
  EEPROM.put(EEPROM_COUNTER_ADDR, soldaCount);
  EEPROM.commit();
  EEPROM.end();
}

void loadCounter() {
  EEPROM.begin(512);
  EEPROM.get(EEPROM_COUNTER_ADDR, soldaCount);
  EEPROM.end();
}

void connectWifi() {
  if (wifiConfig.ssid[0] == 0) {
    Serial.println("‚ùå Nenhuma rede configurada.");
    return;
  }

  Serial.printf("üîó Conectando no Wi-Fi SSID: %s...\n", wifiConfig.ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(wifiConfig.ssid, wifiConfig.pass);
  unsigned long start = millis();
  while (WiFi.status() != WL_CONNECTED && millis() - start < 10000) {
    delay(500);
  }
  if (WiFi.status() == WL_CONNECTED) {
    Serial.println("\n‚úÖ Wi-Fi conectado!");
    Serial.print("üì° IP: ");
    Serial.println(WiFi.localIP());
    checkForUpdate();
  }else{
     Serial.println("\n‚ùå Falha ao conectar no Wi-Fi.");
  }
}

void startConfigPortal() {
  configMode = true;
  WiFi.mode(WIFI_AP);
  WiFi.softAP("SoldaPontoConfig");
  httpUpdater.setup(&server, "/update");
  server.on("/", HTTP_GET, []() {
    String page = "<form method='POST' action='/save'>SSID:<input name='ssid'><br>Senha:<input name='pass' type='password'><br><input type='submit' value='Salvar'></form><p>OTA: <a href='/update'>/update</a></p>";
    server.send(200, "text/html", page);
  });
  server.on("/save", HTTP_POST, []() {
    if (server.hasArg("ssid") && server.hasArg("pass")) {
      String s = server.arg("ssid");
      String p = server.arg("pass");
      s.toCharArray(wifiConfig.ssid, sizeof(wifiConfig.ssid));
      p.toCharArray(wifiConfig.pass, sizeof(wifiConfig.pass));
      saveWifiConfig();
      server.send(200, "text/plain", "Salvo. Reiniciando...");
      delay(1000);
      ESP.restart();
    } else {
      server.send(400, "text/plain", "Falha nos parametros");
    }
  });
  server.begin();
}

//================== ISRs Interrup√ß√µes Externas =======================//
/* Caso qualquer pino do encoder envie sinal, o metodo .tick() sempre ser√°
  chamado, atualizando o valor do encoder via sua biblioteca. */

IRAM_ATTR void checkPosition()// Fun√ß√£o ligada a uma interrup√ß√£o ISR logo n√£o pode retornar valor e deve ser mais r√°pida poss√≠vel
{
  encoder->tick();
}//-------------------------endISR0

unsigned long buttonPressStart = 0;
bool buttonHeld = false;
const unsigned long longPressDuration = 5000; // 5 segundos

void checaBotaoEncoder() {
  bool buttonState = digitalRead(pin_Encoder_SW);

  if (buttonState == LOW) { // Bot√£o pressionado
    if (!buttonHeld) {
      buttonPressStart = millis();
      buttonHeld = true;
    } else {
      if (millis() - buttonPressStart >= longPressDuration) {
        Serial.println("üü¢ Bot√£o pressionado por 5s -> Verificando atualiza√ß√£o OTA...");
        checkForUpdate();
        buttonHeld = false;  // Reseta ap√≥s a atualiza√ß√£o
      }
    }
  } else { // Bot√£o solto
    if (buttonHeld) {
      if (millis() - buttonPressStart < longPressDuration) {
        // Press√£o curta -> troca entre os modos de edi√ß√£o
        if (menuState == NORMAL) {
          menuState = EDITANDO_CICLO;
        } else if (menuState == EDITANDO_CICLO) {
          menuState = EDITANDO_TEMPO;
        } else if (menuState == EDITANDO_TEMPO) {
          menuState = NORMAL;
          saveSettings();
        }
      }
      buttonHeld = false; // Reseta estado do bot√£o
    }
  }
}

int lastEncoderPos = 0;

void atualizaValoresEncoder() {
  encoder->tick();
  int newPos = encoder->getPosition();
  if (newPos != lastEncoderPos) {
    int delta = newPos - lastEncoderPos;
    if (menuState == EDITANDO_CICLO) {
      cycle_ms += delta;
      if (cycle_ms < min_Cycle_ms) cycle_ms = min_Cycle_ms;
      if (cycle_ms > max_Cycle_ms) cycle_ms = max_Cycle_ms;
    } else if (menuState == EDITANDO_TEMPO) {
      time_ms += delta;
      if (time_ms < min_Time_ms) time_ms = min_Time_ms;
      if (time_ms > max_Time_ms) time_ms = max_Time_ms;
    }
    lastEncoderPos = newPos;
  }
}


void atualizaBlink() {
  // Pisca s√≥ em modo edi√ß√£o
  if (menuState == EDITANDO_CICLO || menuState == EDITANDO_TEMPO) {
    if (millis() - lastBlink > 350) {
      showValue = !showValue;
      lastBlink = millis();
    }
  } else {
    showValue = true; // Sempre mostra tudo no modo normal
  }
}

void splashScreen() {
  display.fillScreen(OLED_Color_Black);
  display.setTextColor(OLED_Color_White);

  // Texto principal
  display.setTextSize(2);

  int16_t x1, y1;
  uint16_t w, h;

  display.getTextBounds("Solda", 0, 0, &x1, &y1, &w, &h);
  display.setCursor((SCREEN_WIDTH - w) / 2, 10);
  display.print("Solda");

  display.getTextBounds("Ponto", 0, 0, &x1, &y1, &w, &h);
  display.setCursor((SCREEN_WIDTH - w) / 2, 35);
  display.print("Ponto");

  // Vers√£o
  display.setTextSize(1);
  String versao = "Versao: ";
  versao += FIRMWARE_VERSION;

  display.getTextBounds(versao, 0, 0, &x1, &y1, &w, &h);
  display.setCursor((SCREEN_WIDTH - w) / 2, 55);
  display.print(versao);

  delay(2000);
}

String getWifiStatus() {
  if (WiFi.status() == WL_CONNECTED) {
    return "WiFi OK";
  } else {
    return "Sem WiFi";
  }
}

void setup()
{
  Serial.begin(74880);

  encoder = new RotaryEncoder(pin_Encoder_CLK, pin_Encoder_DT, RotaryEncoder::LatchMode::TWO03);
  //Configura pino como sa√≠da
  digitalWrite(pin_Triac, LOW);
  pinMode(pin_Triac, OUTPUT);
  //Configura pino como entrada PULL-UP
  pinMode(pin_Encoder_SW, INPUT_PULLUP);
  //Configura pino como entrada PULL-UP
  pinMode(pin_Trigger, INPUT_PULLUP);

  pinMode(pin_Encoder_CLK, INPUT_PULLUP);
  pinMode(pin_Encoder_DT, INPUT_PULLUP);

  loadWifiConfig();
  if (digitalRead(pin_Encoder_SW) == LOW) {
    startConfigPortal();
  } else {
    connectWifi();
  }

  loadSettings();
  loadCounter();

  //================= Interrup√ß√£o Externa ========================//
  /* Vincula duas interrup√ß√µes externas no pino 2 e 3 nas fun√ß√µes ISR0 e ISR1
     para garantir que o encoder sempre seja lido com prioridade.*/
  attachInterrupt(digitalPinToInterrupt(0), checkPosition, CHANGE);
  attachInterrupt(digitalPinToInterrupt(2), checkPosition, CHANGE);

  EncoderOne.setPosition(1);
  
  Serial.println("Display Iniciado...");
  display.begin();
  display.setFont();
  splashScreen();
  display.fillScreen(OLED_Background_Color);
}//endSetup --------------------------------------


void loop()
{
  if (configMode) {
    server.handleClient();
  }

  trigger();
  checaBotaoEncoder();
  atualizaValoresEncoder();
  atualizaBlink();
  screenOne();
}//end_void_loop ----------------------



void trigger()
{

  if (digitalRead(pin_Trigger) ) //Se o bot√£o est√° solto
  {
    aux2 = 1;
  }

  if (!digitalRead(pin_Trigger) && aux2 == 1) //Se o bot√£o est√° pressionado
  {


    digitalWrite(pin_Triac, HIGH);
    Serial.println("Ativado");

    // Barra de progresso
    const int barX = 10;
    const int barY = 50;
    const int barWidth = 76;
    const int barHeight = 10;

    display.drawRect(barX - 1, barY - 1, barWidth + 2, barHeight + 2, OLED_Color_White);
    unsigned long start = millis();

    while (millis() - start < time_ms) {
      float progress = (float)(millis() - start) / (float)time_ms;
      if (progress > 1.0) progress = 1.0;
      int filled = progress * barWidth;

      display.fillRect(barX, barY, filled, barHeight, OLED_Color_Green);
      display.fillRect(barX + filled, barY, barWidth - filled, barHeight, OLED_Background_Color);

      delay(10);
    }

    digitalWrite(pin_Triac, LOW);
    Serial.println("Desativado");

    display.fillRect(barX - 1, barY - 1, barWidth + 2, barHeight + 2, OLED_Background_Color);
    soldaCount++;
    saveCounter();
    aux2 = 0;
    delay(500);
  }


}//----------------------- end_selecionaTela

void screenOne()
{
  
  String wifiStatus = (WiFi.status() == WL_CONNECTED) ? "[WiFi]" : "[ X ]";
  
  if (!digitalRead(pin_Encoder_SW) ) //Se o bot√£o est√° solto
  {
    Serial.println("Rotary pressionado");
  }

  static int pos = 0;

  encoder->tick(); // just call tick() to check the state.

  int newPos = encoder->getPosition();

  //valorEncoder = EncoderOne.getPosition();//Captura o valor do encoder
  if (pos != newPos) {
    Serial.print("pos:");
    Serial.print(newPos);
    Serial.print(" dir:");
    Serial.println((int)(encoder->getDirection()));
    pos = newPos;
  } // if

  static bool desenhouTitulos = false;
  if (!desenhouTitulos) {
    display.fillScreen(OLED_Background_Color);
    display.setTextColor(OLED_Text_Color);
    display.setTextSize(1);
    display.setCursor(1, 1);
    display.print("Ciclo:");
    display.setCursor(80, 1);
    display.print("cl");
    display.setCursor(1, 25);
    display.print("Tempo:");
    display.setCursor(80, 25);
    display.print("ms");
    display.setCursor(40, 55);
    display.print(getWifiStatus());
    desenhouTitulos = true;
  }

  // Limpa s√≥ a √°rea dos valores
  if(last_cycle_ms != cycle_ms){
    display.fillRect(35, 1, 45, 16, OLED_Background_Color);
    last_cycle_ms = cycle_ms;
  }

  if(last_time_ms != time_ms){
    display.fillRect(35, 25, 45, 16, OLED_Background_Color);
    last_time_ms = time_ms;
  }

    // Exibe ciclo (piscando s√≥ se editando)
  if (!(menuState == EDITANDO_CICLO && !showValue)) {
    
    if (cycle_ms <= 9) display.setCursor(67, 1);
    else if (cycle_ms >= 10 && cycle_ms <= 99) display.setCursor(55, 1);
    else if (cycle_ms >= 100) display.setCursor(43, 1);

    display.setTextSize(2);
    display.setTextColor(OLED_Text_Color);
    display.print(cycle_ms);
  }

  // Exibe tempo (piscando s√≥ se editando)
  if (!(menuState == EDITANDO_TEMPO && !showValue)) {

    if (time_ms <= 9) display.setCursor(67, 25);
    else if (time_ms >= 10 && time_ms <= 99) display.setCursor(55, 25);
    else if (time_ms >= 100) display.setCursor(43, 25);
    
    display.setTextSize(2);
    display.setTextColor(OLED_Text_Color);
    display.print(time_ms);
  }

   if (last_soldaCount != soldaCount || wifiStatus != last_wifi_status) {
    // Limpa √°rea inferior
    display.fillRect(0, 50, SCREEN_WIDTH, 14, OLED_Background_Color);

    display.setTextSize(1);
    display.setCursor(1, 55);
    display.print(wifiStatus);

    display.setCursor(60, 55);
    display.print("S:");
    display.print(soldaCount);

    last_soldaCount = soldaCount;
    last_wifi_status = wifiStatus;
  }

}//end_screenOne ----------------------